// @strict-types


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Записать.
// 
// Параметры:
//  ИспользуемыеПроектыДоски - см. канбан_КанбанДоска.НовыйПроектыДоски
//  Отказ - Булево
//
Процедура Записать(ИспользуемыеПроектыДоски, Отказ) Экспорт
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.канбан_ИспользуемыеПроектыДоски");
		ЭлементБлокировки.УстановитьЗначение("Пользователь", ТекущийПользователь);
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Пользователь.Установить(ТекущийПользователь);
		
		Для Каждого ДанныеПроекта Из ИспользуемыеПроектыДоски Цикл
			
			СтрокаЗаписи = НаборЗаписей.Добавить();
			СтрокаЗаписи.Пользователь = ТекущийПользователь;
			СТрокаЗаписи.Проект = ДанныеПроекта.Проект;
			СтрокаЗаписи.ИдентификаторПроектаСтрока = Строка(ДанныеПроекта.Проект.УникальныйИдентификатор());
			СтрокаЗаписи.Цвет = ДанныеПроекта.Цвет;
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
		Отказ = Истина;
		
		ЗаписьЖурналаРегистрации(
			"Запись регистра сведений канбан_ИспользуемыеПроектыДоски для проекта: ", 
			УровеньЖурналаРегистрации.Ошибка,, 
			ДанныеПроекта.Проект, 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Загрузить.
// 
// Параметры:
//  ПроектыДоски - см. канбан_КанбанДоска.НовыйПроектыДоски
//
Процедура Загрузить(ПроектыДоски) Экспорт
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(ТекущийПользователь);
	НаборЗаписей.Прочитать();
	
	Для Каждого СтрокаНабора Из НаборЗаписей Цикл
		
		СтрокаПроектыДоски = ПроектыДоски.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПроектыДоски, СтрокаНабора);
		СтрокаПроектыДоски.ПредставлениеПроекта = Строка(СтрокаНабора.Проект);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли
