// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Статус.Пустая() Тогда
		Объект.Статус = Параметры.Статус; // ОпределяемыйТип.канбан_СтатусыЗадач
	КонецЕсли;
	
	канбан_КанбанДоска.ТекстHTMLРедактора(СтраницаHTMLРедактора, УникальныйИдентификатор);
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ИмяПользователя = ТекущийПользователь.ПолноеНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "канбан_ИзменениеСтатусаЗадаче" Тогда
		ОбновитьОбъект();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтраницаHTMLРедактораПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СсылкаВЭлементеСтраницы = ДанныеСобытия.Href; // Строка
	Кнопка = ДанныеСобытия.Button; // ВнешнийОбъект
	
	Если ЗначениеЗаполнено(СсылкаВЭлементеСтраницы) Тогда // Это ссылка из текста задачи
		
		СтандартнаяОбработка = Ложь;
		
		СсылкаИБ = ПолучитьНавигационнуюСсылкуИнформационнойБазы();
		Если СтрНайти(СсылкаВЭлементеСтраницы, СсылкаИБ) Тогда
			
			ВнутренняяСсылка = СтрЗаменить(СсылкаВЭлементеСтраницы, СсылкаИБ, "");
			ВнутренняяСсылка = СтрЗаменить(ВнутренняяСсылка, "#", "");
			ПерейтиПоНавигационнойСсылке(ВнутренняяСсылка);
			
		Иначе
			
			ЗапуститьПриложение(СсылкаВЭлементеСтраницы);
			
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Кнопка) Тогда // Это элемент верстки
		
		Если Кнопка.id = "V8_request" Тогда
			
			Событие = Кнопка.value;
			
			Если Событие = "importTasks" Тогда
				
				канбан_КанбанДоскаКлиент.ОтправитьОтвет(Элемент, Событие, Объект.Содержание, ИмяПользователя);
				
			ИначеЕсли Событие = "exportTasks" Тогда
				
				// Запись текста содержания задачи в объект.
				Объект.Содержание = Кнопка.textContent;
				Модифицированность = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьОбъект()
	
	Прочитать();
	
КонецПроцедуры

#КонецОбласти