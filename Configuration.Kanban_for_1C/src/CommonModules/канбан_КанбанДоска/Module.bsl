// @strict-types


#Область ПрограммныйИнтерфейс

// Получить текст HTML.
// 
// Параметры:
//  ПоказыватьФотоПользователя - Булево - Показывать фото пользователя
// 
// Возвращаемое значение:
//  Строка - Текст HTMLКанбан доски
//
Функция ТекстHTMLКанбанДоски(ПоказыватьФотоПользователя = Истина) Экспорт
	
	ТекстHTML = Обработки.Канбан_Доска_HTML.ТекстHTMLКанбанДоски(ПоказыватьФотоПользователя);
	Возврат ТекстHTML;
	
КонецФункции

// Канбан доска HTMLВключена.
// 
// Возвращаемое значение:
//   Булево - Канбан доска HTMLВключена
//
Функция КанбанДоскаHTMLВключенаЗагрузить() Экспорт
	
	КлючОбъекта = КлючОбъекта();
	КлючНастроек = КлючНастроекДоскаВключена();
	Результат = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбъекта, КлючНастроек, Ложь); // Булево
	
	Возврат Результат;
	
КонецФункции

// Канбан доска HTMLПри изменении.
// 
// Параметры:
//  Значение - Булево
//  Отказ - Булево
//
Процедура КанбанДоскаHTMLВключенаСохранить(Значение, Отказ) Экспорт
	
	КлючОбъекта = КлючОбъекта();
	КлючНастроек = КлючНастроекДоскаВключена();
	
	Попытка
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючОбъекта, КлючНастроек, Значение);
		
	Исключение
		
		ЗаписьЖурналаРегистрации(
			"Запись настроек канбан доски HTML", 
			УровеньЖурналаРегистрации.Ошибка,, 
			"Не удалось записать настройку включения", 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Используемые проекты доски сохранить.
// 
// Параметры:
//  ИспользуемыеПроектыДоски - см. НовыйПроектыДоски
//  Отказ - Булево
//
Процедура ИспользуемыеПроектыДоскиСохранить(ИспользуемыеПроектыДоски, Отказ) Экспорт
	
	РегистрыСведений.канбан_ИспользуемыеПроектыДоски.Записать(ИспользуемыеПроектыДоски, Отказ);
	
КонецПроцедуры

// Используемые проекты доски загрузить.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см. НовыйПроектыДоски
//
Функция ИспользуемыеПроектыДоскиЗагрузить() Экспорт
	
	ПроектыДоски = НовыйПроектыДоски();
	
	РегистрыСведений.канбан_ИспользуемыеПроектыДоски.Загрузить(ПроектыДоски);
	
	Возврат ПроектыДоски;
	
КонецФункции

// Используемые статусы доски сохранить.
// 
// Параметры:
//  ИспользуемыеСтатусыДоски - см. НовыйСтатусыДоски
//  Отказ - Булево
//
Процедура ИспользуемыеСтатусыДоскиСохранить(ИспользуемыеСтатусыДоски, Отказ) Экспорт
	
	РегистрыСведений.канбан_ИспользуемыеСтатусыДоски.Записать(ИспользуемыеСтатусыДоски, Отказ);
	
КонецПроцедуры

// Используемые статусы доски сохраненные для текущего пользователя
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см. НовыйСтатусыДоски
//
Функция ИспользуемыеСтатусыДоскиЗагрузить() Экспорт
	
	СтатусыДоски = НовыйСтатусыДоски();
	
	РегистрыСведений.канбан_ИспользуемыеСтатусыДоски.Загрузить(СтатусыДоски);
	
	Возврат СтатусыДоски;
	
КонецФункции

// Изменить статус задаче.
// 
// Параметры:
//  ИдентификаторЗадачаСтрока - Строка
//  ИдентификаторСтатусаСтрока - Строка
//  Отказ - Булево
//
// Возвращаемое значение:
//  ОпределяемыйТип.канбан_Задачи
//
Функция ИзменитьСтатусЗадаче(ИдентификаторЗадачаСтрока, ИдентификаторСтатусаСтрока, Отказ) Экспорт
	
	#Область Задача
	
	ИдентификаторЗадача = Новый УникальныйИдентификатор(ИдентификаторЗадачаСтрока);
	
	ТипыЗадачи = Метаданные.ОпределяемыеТипы.канбан_Задачи.Тип.Типы();
	ЗадачаСсылка = Новый(ТипыЗадачи[0]); // ОпределяемыйТип.канбан_Задачи
	ПолноеИмя = ЗадачаСсылка.Метаданные().ПолноеИмя();
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя); // СправочникМенеджер
	
	ЗадачаСсылка = МенеджерОбъекта.ПолучитьСсылку(ИдентификаторЗадача); // ОпределяемыйТип.канбан_Задачи
	
	#КонецОбласти
	
	#Область Статус
	
	ИдентификаторСтатуса = Новый УникальныйИдентификатор(ИдентификаторСтатусаСтрока);
	
	ТипыЗадачи = Метаданные.ОпределяемыеТипы.канбан_СтатусыЗадач.Тип.Типы();
	СтатусСсылка = Новый(ТипыЗадачи[0]); // ОпределяемыйТип.канбан_СтатусыЗадач
	ПолноеИмя = СтатусСсылка.Метаданные().ПолноеИмя();
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя); // СправочникМенеджер
	
	СтатусСсылка = МенеджерОбъекта.ПолучитьСсылку(ИдентификаторСтатуса); // ОпределяемыйТип.канбан_СтатусыЗадач
	
	#КонецОбласти
	
	канбан_КанбанДоскаПереопределяемый.ИзменитьСтатусЗадаче(ЗадачаСсылка, СтатусСсылка, Отказ);
	
	Возврат ЗадачаСсылка;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Данные для канбан доски.
// 
// Возвращаемое значение:
//  см. НовыйДанныеДляКанбанДоски
//
Функция ДанныеДляКанбанДоски() Экспорт
	
	ТекстЗапросаДанныеЗадач = канбан_КанбанДоскаПереопределяемый.ТекстЗапросаДанныеЗадач();
	
	ДанныеДляКанбанДоски = НовыйДанныеДляКанбанДоски();
	
	ДанныеДляКанбанДоски.Проекты = ИспользуемыеПроектыДоскиЗагрузить();
	
	// TODO вынести в отдельную процедуру
	Если ДанныеДляКанбанДоски.Проекты.Количество() = 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаДанныеЗадач;
		РезультатЗапроса = Запрос.Выполнить();
		ТаблицаПроектов = РезультатЗапроса.Выгрузить();
		ТаблицаПроектов.Свернуть("Проект");
		Для Каждого СтрокаПроекта Из ТаблицаПроектов Цикл
			
			НоваяСтрокаПроектов = ДанныеДляКанбанДоски.Проекты.Добавить();
			НоваяСтрокаПроектов.Проект = СтрокаПроекта.Проект;
			НоваяСтрокаПроектов.ПредставлениеПроекта = СтрокаПроекта.Проект.Наименование;
			НоваяСтрокаПроектов.ИдентификаторПроектаСтрока = Строка(СтрокаПроекта.Проект.УникальныйИдентификатор());
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПараметрыОтбора = НовыйПараметрыОтбораЗадач();
	
	ПараметрыОтбора.Проекты = ДанныеДляКанбанДоски.Проекты.ВыгрузитьКолонку("Проект"); // Массив из ОпределяемыйТип.канбан_Проекты
	
	ТекстЗапросаСтатусы = канбан_КанбанДоскаПереопределяемый.ТекстЗапросаСтатусы();
	РегистрыСведений.канбан_ИспользуемыеСтатусыДоски.Статусы(ТекстЗапросаСтатусы, ДанныеДляКанбанДоски);
	
	Выборка = ДополнитьЗапросДанныеЗадач(ТекстЗапросаДанныеЗадач, ПараметрыОтбора);
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ДанныеДляКанбанДоски.ЗадачиИСтатусы.Добавить();
		НоваяСтрока.Задача = Выборка.Задача;
		НоваяСтрока.Статус = Выборка.СтатусЗадачи;
		
		КартинкаПоУмолчанию = БиблиотекаКартинок.канбан_ФотоПоУмолчанию;
		ХранилищеФотографии = Выборка.ХранилищеФотографии;
		Если ПустаяСтрока(ХранилищеФотографии) 
			Или ХранилищеФотографии.Получить() =  Неопределено Тогда
			
			ДвоичныеДанныеФотографии = КартинкаПоУмолчанию.ПолучитьДвоичныеДанные();
			
		Иначе
			
			ДвоичныеДанныеФотографии = ХранилищеФотографии.Получить();
			
		КонецЕсли;
		
		ФотографияСтрокаBase64 = Base64Строка(ДвоичныеДанныеФотографии);
		НоваяСтрока.ФотографияИсполнителя = СтрШаблон("data:image/jpeg;base64,%1", ФотографияСтрокаBase64);
		
		НоваяСтрока.Наименование = Выборка.НаименованиеЗадачи;
		
		СсылкаНаОбъект = ПолучитьНавигационнуюСсылку(Выборка.Задача);
		СсылкаНаИБ = ПолучитьНавигационнуюСсылкуИнформационнойБазы();
		НоваяСтрока.НавигационнаяСсылка = СтрШаблон("%1#%2", СсылкаНаИБ, СсылкаНаОбъект);
		
		НоваяСтрока.ИдентификаторЗадача = Строка(Выборка.Задача.УникальныйИдентификатор());
		НоваяСтрока.Код = Выборка.КодЗадачи;
		НоваяСтрока.Проект = Выборка.Проект;
		НоваяСтрока.ПредставлениеПроекта = Строка(Выборка.Проект);
		НоваяСтрока.ЦветПроекта = Выборка.ЦветПроекта;
		НоваяСтрока.ИдентификаторПроекта = Выборка.ИдентификаторПроектаСтрока;
		НоваяСтрока.ИдентификаторПользователя = 
			?(Выборка.Исполнитель.Пустая(), "", Строка(Выборка.Исполнитель.УникальныйИдентификатор()));
		
	КонецЦикла;
	
	Возврат ДанныеДляКанбанДоски;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Новый параметры отбора задач.
// 
// Возвращаемое значение:
//  Структура - Новый параметры отбора задач:
//  * Проекты - Массив из ОпределяемыйТип.канбан_Проекты
//  * ТекущийПользователь - ОпределяемыйТип.канбан_Пользователь
//
Функция НовыйПараметрыОтбораЗадач() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Проекты", Новый Массив);
	Результат.Вставить("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	Возврат Результат;
	
КонецФункции

// Новый данные для канбан доски.
// 
// Возвращаемое значение:
//  Структура:
// * Статусы - см. НовыйСтатусыДоски
// * ЗадачиИСтатусы - см. НовыйЗадачиИСтатусы
// * Проекты - см. НовыйПроектыДоски
//
Функция НовыйДанныеДляКанбанДоски() Экспорт
	
	СтатусыДоски = НовыйСтатусыДоски();
	
	ЗадачиИСтатусы = НовыйЗадачиИСтатусы();
	
	Проекты = НовыйПроектыДоски();
	
	ДанныеДляКанбанДоски = Новый Структура;
	ДанныеДляКанбанДоски.Вставить("Статусы", СтатусыДоски);
	ДанныеДляКанбанДоски.Вставить("ЗадачиИСтатусы", ЗадачиИСтатусы);
	ДанныеДляКанбанДоски.Вставить("Проекты", Проекты);
	
	Возврат ДанныеДляКанбанДоски;
	
КонецФункции

// Новый статусы доски.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новый статусы доски:
// * СтатусЗадачи - ОпределяемыйТип.канбан_СтатусыЗадач
// * НомерПоПорядку - Число
// * КнопкаДобавить - Булево
// * Представление - Строка
//
Функция НовыйСтатусыДоски() Экспорт
	
	СтатусыДоски = Новый ТаблицаЗначений;
	СтатусыДоски.Колонки.Добавить("СтатусЗадачи", Метаданные.ОпределяемыеТипы.канбан_СтатусыЗадач.Тип);
	СтатусыДоски.Колонки.Добавить("НомерПоПорядку", Новый ОписаниеТипов("Число"));
	СтатусыДоски.Колонки.Добавить("КнопкаДобавить", Новый ОписаниеТипов("Булево"));
	СтатусыДоски.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	
	Возврат СтатусыДоски;
	
КонецФункции

// Новый задачи и статусы.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новый задачи и статусы:
// * Задача - ОпределяемыйТип.канбан_Задачи
// * Наименование - Строка
// * Код - Строка
// * Статус - ОпределяемыйТип.канбан_СтатусыЗадач
// * ФотографияИсполнителя - Строка
// * ИдентификаторПользователя - Строка
// * НавигационнаяСсылка - Строка
// * ИдентификаторЗадача - Строка
// * Проект - ОпределяемыйТип.канбан_Проекты
// * ПредставлениеПроекта - Строка
// * ИдентификаторПроекта - Строка
// * ЦветПроекта - Строка
//
Функция НовыйЗадачиИСтатусы()
	
	ЗадачиИСтатусы = Новый ТаблицаЗначений;
	ЗадачиИСтатусы.Колонки.Добавить("Задача", Метаданные.ОпределяемыеТипы.канбан_Задачи.Тип);
	ЗадачиИСтатусы.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("Статус", Метаданные.ОпределяемыеТипы.канбан_СтатусыЗадач.Тип);
	ЗадачиИСтатусы.Колонки.Добавить("ФотографияИсполнителя", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("ИдентификаторПользователя", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("НавигационнаяСсылка", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("ИдентификаторЗадача", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("Проект", Метаданные.ОпределяемыеТипы.канбан_Проекты.Тип);
	ЗадачиИСтатусы.Колонки.Добавить("ПредставлениеПроекта", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("ИдентификаторПроекта", Новый ОписаниеТипов("Строка"));
	ЗадачиИСтатусы.Колонки.Добавить("ЦветПроекта", Новый ОписаниеТипов("Строка"));
	
	Возврат ЗадачиИСтатусы;
	
КонецФункции

// Новый проекты доски.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новый проекты доски:
// * Проект - ОпределяемыйТип.канбан_Проекты
// * Цвет - Строка
// * ИдентификаторПроектаСтрока - Строка
// * ПредставлениеПроекта - Строка
//
Функция НовыйПроектыДоски() Экспорт
	
	ПроектыДоски = Новый ТаблицаЗначений();
	ПроектыДоски.Колонки.Добавить("Проект", Метаданные.ОпределяемыеТипы.канбан_Проекты.Тип);
	ПроектыДоски.Колонки.Добавить("Цвет", Новый ОписаниеТипов("Строка"));
	ПроектыДоски.Колонки.Добавить("ИдентификаторПроектаСтрока", Новый ОписаниеТипов("Строка"));
	ПроектыДоски.Колонки.Добавить("ПредставлениеПроекта", Новый ОписаниеТипов("Строка"));
	
	Возврат ПроектыДоски;
	
КонецФункции

Функция КлючОбъекта()
	Возврат "КанбанДоскаHTML";
КонецФункции

Функция КлючНастроекДоскаВключена()
	Возврат "КанбанДоскаHTMLВключена";
КонецФункции

// Дополнить запрос данные задач.
// 
// Параметры:
//  ТекстЗапросаДанныеЗадач - см. канбан_КанбанДоскаПереопределяемый.ТекстЗапросаДанныеЗадач
//  ПараметрыОтбора - см. канбан_КанбанДоска.НовыйПараметрыОтбораЗадач
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
// * Задача - ОпределяемыйТип.канбан_Задачи - ссылка на задачу
// * Код - Строка
// * СтатусЗадачи - ОпределяемыйТип.канбан_СтатусыЗадач
// * ХранилищеФотографии - Строка, ХранилищеЗначения - содержит фотографию исполнителя задачи.
// * ИдентификаторПользователя - УникальныйИдентификатор
// * НаименованиеЗадачи - Строка - наименование задачи
// * КодЗадачи - Число, Строка - код задачи
// * ИдентификаторЗадачи - УникальныйИдентификатор
// * Проект - ОпределяемыйТип.канбан_Проекты
// * ЦветПроекта - Строка
// * ИдентификаторПроектаСтрока - Строка
// * Исполнитель - ОпределяемыйТип.канбан_Пользователь
//
Функция ДополнитьЗапросДанныеЗадач(ТекстЗапросаДанныеЗадач, ПараметрыОтбора)
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапросаДанныеЗадач);
	ПервыйПакет = СхемаЗапроса.ПакетЗапросов.Получить(0);
	Оператор = ПервыйПакет.Операторы[0];
	
	#Область ДанныеСтатусов
	
	// Соединяем с таблицей используемых статусов, отсекаем лишние задачи.
	ИспользуемыеСтатусыДоски = Оператор.Источники.Добавить(
		"РегистрСведений.канбан_ИспользуемыеСтатусыДоски", "ИспользуемыеСтатусыДоски");
	
	Оператор.Источники[3].Соединения.Очистить();
	
	// TODO тут нужно заменить условие, т.к. я не знаю что есть справочник"узЗадачи.Проект" нужно искать это поле по наименованию
	Оператор.Источники[0].Соединения.Добавить(
		ИспользуемыеСтатусыДоски, 
		"узЗадачи.Статус = ИспользуемыеСтатусыДоски.СтатусЗадачи И ИспользуемыеСтатусыДоски.Пользователь = &ТекущийПользователь");
	
	Оператор.Источники[0].Соединения[2].ТипСоединения = ТипСоединенияСхемыЗапроса.ВНУТРЕННЕЕ;
	
	#КонецОбласти
	
	#Область ДанныеПроектов
	
	ИспользуемыеПроектыДоски = Оператор.Источники.Добавить(
		"РегистрСведений.канбан_ИспользуемыеПроектыДоски", "ИспользуемыеПроектыДоски");
	
	Оператор.Источники[4].Соединения.Очистить();
	
	// TODO тут нужно заменить условие, т.к. я не знаю что есть справочник"узЗадачи.Проект" нужно искать это поле по наименованию
	Оператор.Источники[0].Соединения.Добавить(
		ИспользуемыеПроектыДоски, 
		"ЕСТЬNULL(узЗадачиОсновные.Проект, узЗадачи.Проект) = ИспользуемыеПроектыДоски.Проект И ИспользуемыеПроектыДоски.Пользователь = &ТекущийПользователь");
	
	Оператор.Источники[0].Соединения[3].ТипСоединения = ТипСоединенияСхемыЗапроса.ЛевоеВнешнее; // Получаем все проекты, отберем их условием
	
	Оператор.ВыбираемыеПоля.Добавить("ЕСТЬNULL(ИспользуемыеПроектыДоски.Цвет, """")", 7);
	ПервыйПакет.Колонки[7].Псевдоним = "ЦветПроекта";
	
	Оператор.ВыбираемыеПоля.Добавить("УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ЕСТЬNULL(узЗадачиОсновные.Проект, узЗадачи.Проект))", 8);
	ПервыйПакет.Колонки[8].Псевдоним = "ИдентификаторПроектаСтрока";
	
	// TODO тут нужно заменить условие, т.к. я не знаю что есть справочник"узЗадачи.Проект" нужно искать это поле по наименованию
	Оператор.Отбор.Добавить("ЕСТЬNULL(узЗадачиОсновные.Проект, узЗадачи.Проект) В (&Проекты)");
	
	#КонецОбласти
	
	Запрос = Новый Запрос;
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Запрос.УстановитьПараметр("Проекты", ПараметрыОтбора.Проекты);
	Запрос.УстановитьПараметр("ТекущийПользователь", ПараметрыОтбора.ТекущийПользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Возврат ВыборкаДетальныеЗаписи;
	
КонецФункции

#КонецОбласти
