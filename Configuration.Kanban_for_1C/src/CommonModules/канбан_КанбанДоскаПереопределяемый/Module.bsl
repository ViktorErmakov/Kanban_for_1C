// @strict-types


#Область СлужебныйПрограммныйИнтерфейс

// Текст запроса данные задач.
// 
// Возвращаемое значение:
//  Строка - Текст запроса данные задач
//  
//  Запрос должен вернуть поля:
// * Задача - ОпределяемыйТип.канбан_Задачи - ссылка на задачу
// * КодЗадачи - Число, Строка - код задачи
// * СтатусЗадачи - ОпределяемыйТип.канбан_СтатусыЗадач
// * ХранилищеФотографии - ХранилищеЗначения - фотография исполнителя задачи.
// * НаименованиеЗадачи - Строка - наименование задачи
// * Проект - ОпределяемыйТип.канбан_Проекты
// * Исполнитель - ОпределяемыйТип.канбан_Пользователь
// * КодПроекта - Строка
//
Функция ТекстЗапросаДанныеЗадач() Экспорт
	
	#Область КонфигурацииНаБазеБСП_Tasks
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Задачи.Ссылка КАК Задача,
		|	Задачи.Код КАК КодЗадачи,
		|	Задачи.Статус КАК СтатусЗадачи,
		|	ЕСТЬNULL(Пользователи.Фотография, """") КАК ХранилищеФотографии,
		|	Задачи.Наименование КАК НаименованиеЗадачи,
		|	ЕСТЬNULL(ЗадачиОсновные.Проект, Задачи.Проект) КАК Проект,
		|	Задачи.Исполнитель КАК Исполнитель,
		|	ЕСТЬNULL(ЗадачиОсновные.Проект.Код, Задачи.Проект.Код) КАК КодПроекта
		|ИЗ
		|	&ОбъектЗапроса КАК Задачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО Задачи.Исполнитель = Пользователи.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ &ОбъектЗапроса КАК ЗадачиОсновные
		|		ПО Задачи.ОсновнаяЗадача = ЗадачиОсновные.Ссылка
		|ГДЕ
		|	Задачи.ПоказыватьВОтчетахИКанбанДоске
		|
		|УПОРЯДОЧИТЬ ПО
		|	КодПроекта,
		|	КодЗадачи";
	
	канбан_КанбанДоска.УстановитьОбъектЗапроса(Запрос.Текст, "канбан_Задачи");
	
	#КонецОбласти
	
	Возврат Запрос.Текст;
	
КонецФункции

// Текст запроса статусы.
// Запрос должен вернуть заполненные поля:
// * Статус - ОпределяемыйТип.канбан_СтатусыЗадач
// * Представление - Строка - представление статуса для канбан доски
// * УникальныйИдентификатор - Строка
// 
// Возвращаемое значение:
//  Строка - Текст запроса статусы
//
Функция ТекстЗапросаСтатусы() Экспорт
	
	#Область КонфигурацииНаБазеБСП_Tasks
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтатусыЗадачи.Ссылка КАК Статус,
		|	ВЫБОР
		|		КОГДА СтатусыЗадачи.НаименованиеДляКанбанДоски = """"
		|			ТОГДА СтатусыЗадачи.Наименование
		|		ИНАЧЕ СтатусыЗадачи.НаименованиеДляКанбанДоски
		|	КОНЕЦ КАК Представление,
		|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(СтатусыЗадачи.Ссылка) КАК УникальныйИдентификатор
		|ИЗ
		|	&ОбъектЗапроса КАК СтатусыЗадачи";
	
	канбан_КанбанДоска.УстановитьОбъектЗапроса(Запрос.Текст, "канбан_СтатусыЗадач");
	
	#КонецОбласти
	
	Возврат Запрос.Текст;
	
КонецФункции

// Изменить статус задаче.
// 
// Параметры:
//  Задача - ОпределяемыйТип.канбан_Задачи
//  Статус - ОпределяемыйТип.канбан_СтатусыЗадач - устанавливаемый статус
//  Отказ - Булево
//
Процедура ИзменитьСтатусЗадаче(Задача, Статус, Отказ) Экспорт
	
	#Область Tasks_КонфигурацииНаБазеБСП
	
	Попытка
		
		ЗадачаОбъект = Задача.ПолучитьОбъект();
		
		ЗадачаОбъект.Заблокировать();
		
		ЗадачаОбъект.Статус = Статус;
		
		ЗадачаОбъект.Записать();
		
	Исключение
		
		Отказ = Истина;
		
		ЗаписьЖурналаРегистрации(
			"ТипОперации", 
			УровеньЖурналаРегистрации.Ошибка,, 
			"Текст", 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
		
	КонецПопытки;
	
	#КонецОбласти
	
КонецПроцедуры

#КонецОбласти