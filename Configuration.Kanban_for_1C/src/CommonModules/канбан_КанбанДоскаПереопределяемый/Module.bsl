// @strict-types


#Область СлужебныйПрограммныйИнтерфейс

// Текст запроса данные задач.
// 
// Возвращаемое значение:
//  Строка - Текст запроса данные задач
//  
//  Запрос должен вернуть поля:
// * Задача - ОпределяемыйТип.канбан_Задачи - ссылка на задачу
// * КодЗадачи - Число, Строка - код задачи
// * СтатусЗадачи - ОпределяемыйТип.канбан_СтатусыЗадач
// * ХранилищеФотографии - ХранилищеЗначения - фотография исполнителя задачи.
// * НаименованиеЗадачи - Строка - наименование задачи
// * Проект - ОпределяемыйТип.канбан_Проекты
// * Исполнитель - ОпределяемыйТип.канбан_Пользователь
//
Функция ТекстЗапросаДанныеЗадач() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	узЗадачи.Ссылка КАК Задача,
		|	узЗадачи.Код КАК КодЗадачи,
		|	узЗадачи.Статус КАК СтатусЗадачи,
		|	ЕСТЬNULL(Пользователи.Фотография, """") КАК ХранилищеФотографии,
		|	узЗадачи.Наименование КАК НаименованиеЗадачи,
		|	ЕСТЬNULL(узЗадачиОсновные.Проект, узЗадачи.Проект) КАК Проект,
		|	узЗадачи.Исполнитель КАК Исполнитель
		|ИЗ
		|	Справочник.узЗадачи КАК узЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО узЗадачи.Исполнитель = Пользователи.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.узЗадачи КАК узЗадачиОсновные
		|		ПО узЗадачи.ОсновнаяЗадача = узЗадачиОсновные.Ссылка
		|ГДЕ
		|	узЗадачи.ПоказыватьВОтчетахИКанбанДоске";
	
	Возврат Запрос.Текст;
	
КонецФункции

// Текст запроса статусы.
// Запрос должен вернуть заполненные поля:
// * Статус - ОпределяемыйТип.канбан_СтатусыЗадач
// * Представление - Строка - представление статуса для канбан доски
// * УникальныйИдентификатор - Строка
// 
// Возвращаемое значение:
//  Строка - Текст запроса статусы
//
Функция ТекстЗапросаСтатусы() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	узСтатусыЗадачи.Ссылка КАК Статус,
		|	ВЫБОР
		|		КОГДА узСтатусыЗадачи.НаименованиеДляКанбанДоски = """"
		|			ТОГДА узСтатусыЗадачи.Наименование
		|		ИНАЧЕ узСтатусыЗадачи.НаименованиеДляКанбанДоски
		|	КОНЕЦ КАК Представление,
		|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(узСтатусыЗадачи.Ссылка) КАК УникальныйИдентификатор
		|ИЗ
		|	Справочник.узСтатусыЗадачи КАК узСтатусыЗадачи";
	
	Возврат Запрос.Текст;
	
КонецФункции

// Изменить статус задаче.
// 
// Параметры:
//  Задача - ОпределяемыйТип.канбан_Задачи
//  Статус - ОпределяемыйТип.канбан_СтатусыЗадач
//  Отказ - Булево
//
Процедура ИзменитьСтатусЗадаче(Задача, Статус, Отказ) Экспорт
	
	Попытка
		
		ЗадачаОбъект = Задача.ПолучитьОбъект();
		
		Если Не ЗадачаОбъект.Заблокирован() Тогда
			ЗадачаОбъект.Заблокировать();
		КонецЕсли;
		
		ЗадачаОбъект.Статус = Статус;
		
		ЗадачаОбъект.Записать();
		
	Исключение
		
		Отказ = Истина;
		
		ЗаписьЖурналаРегистрации(
			"ТипОперации", 
			УровеньЖурналаРегистрации.Ошибка,, 
			"Текст", 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти