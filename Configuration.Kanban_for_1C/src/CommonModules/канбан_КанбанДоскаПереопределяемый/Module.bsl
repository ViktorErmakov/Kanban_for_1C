// @strict-types


#Область СлужебныйПрограммныйИнтерфейс

// Данные для канбан доски.
// 
// Параметры:
//  ПараметрыОтбора - см. канбан_КанбанДоска.НовыйПараметрыОтбораЗадач
//  ДанныеДляКанбанДоски - см. канбан_КанбанДоска.НовыйДанныеДляКанбанДоски
//
Процедура ДанныеДляКанбанДоски(Знач ПараметрыОтбора, ДанныеДляКанбанДоски) Экспорт
	
	// Заполнение списка статусов для отображения на доске
	Статусы = ДанныеДляКанбанДоски.Статусы;
	
	// TODO временное получение запросом, в дальнейшем нужно сделать настройки для пользователей
	// имеется ввиду какое то соответствие.
	// возможно сделать РС в который добавить статусы которые будут использоваться в системе.
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	узСтатусыЗадачи.Ссылка
		|ИЗ
		|	Справочник.узСтатусыЗадачи КАК узСтатусыЗадачи
		|ГДЕ
		|	НЕ узСтатусыЗадачи.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Статусы.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
	// Заполнение списка задач с пользователем и адресом фотографии его
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	узЗадачи.Ссылка КАК Ссылка,
		|	узЗадачи.Статус КАК Статус,
		|	ЕСТЬNULL(Пользователи.Фотография, """") КАК ХранилищеФотографии,
		|	узЗадачи.Наименование КАК Наименование,
		|	узЗадачи.Код КАК Код
		|ИЗ
		|	Справочник.узЗадачи КАК узЗадачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
		|		ПО узЗадачи.Исполнитель = Пользователи.Ссылка
		|ГДЕ
		|	НЕ узЗадачи.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НоваяСтрока = ДанныеДляКанбанДоски.ЗадачиИСтатусы.Добавить();
		НоваяСтрока.Задача = ВыборкаДетальныеЗаписи.Ссылка;
		НоваяСтрока.Статус = ВыборкаДетальныеЗаписи.Статус;
		ХранилищеФотографии = ВыборкаДетальныеЗаписи.ХранилищеФотографии;
		Если ЗначениеЗаполнено(ХранилищеФотографии) Тогда
			ДвоичныеДанныеФотографии = ХранилищеФотографии.Получить();
			ФотографияСтрокаBase64 = Base64Строка(ДвоичныеДанныеФотографии);
			НоваяСтрока.ФотографияИсполнителя = СтрШаблон("data:image/jpeg;base64,%1", ФотографияСтрокаBase64)
		КонецЕсли;
		НоваяСтрока.Наименование = ВыборкаДетальныеЗаписи.Наименование;
		НоваяСтрока.Код = ВыборкаДетальныеЗаписи.Код;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Код процедур и функций

#КонецОбласти
