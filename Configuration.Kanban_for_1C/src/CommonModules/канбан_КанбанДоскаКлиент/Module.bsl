// @strict-types


#Область ПрограммныйИнтерфейс

// Открыть канбан доску
//
Процедура ОткрытьКанбанДоску() Экспорт
	ОткрытьФорму("Обработка.Канбан_Доска_HTML.Форма.Доска");
КонецПроцедуры

// Открыть страницу разработки.
Процедура ОткрытьСтраницуРазработки() Экспорт
	
	ЗапуститьПриложение("https://github.com/ViktorErmakov/Kanban_for_1C");
	
КонецПроцедуры

// Отправить ответ.
// 
// Параметры:
//  ЭлементПолеHTML - ЭлементФормаHTML
//  Событие - Строка
//  Ответ - Неопределено, Структура, ФиксированнаяСтруктура, Соответствие, ФиксированноеСоответствие, Массив, ФиксированныйМассив, ДвоичныеДанные - ответ для HTML.
//  ИмяПользователя - Строка
//
Процедура ОтправитьОтвет(Знач ЭлементПолеHTML, Знач Событие, Знач Ответ, Знач ИмяПользователя) Экспорт
	
	ЭлементHTML = ЭлементПолеHTML.Документ.defaultView;
	Прокси = ЭлементHTML["V8Proxy"];
	
	Пакет = "";
	Если ЗначениеЗаполнено(Ответ) Тогда
		
		Если    ТипЗнч(Ответ) = Тип("Структура")
			Или ТипЗнч(Ответ) = Тип("ФиксированнаяСтруктура")
			Или ТипЗнч(Ответ) = Тип("Соответствие")
			Или ТипЗнч(Ответ) = Тип("ФиксированноеСоответствие")
			Или ТипЗнч(Ответ) = Тип("Массив")
			Или ТипЗнч(Ответ) = Тип("ФиксированныйМассив") Тогда
			
			Пакет = ЗаписатьЗначениеJSON(Ответ);
			
		ИначеЕсли ТипЗнч(Ответ) = Тип("ДвоичныеДанные") Тогда
			
			Пакет = Base64Строка(Ответ);
			
		Иначе
			
			Пакет = XMLСтрока(Ответ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Прокси.sendResponse(Событие, Пакет, ИмяПользователя);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Открыть форму списка определяемого типа.
// 
// Параметры:
//  ИмяОпределяемогоТипа - Строка
//
Процедура ОткрытьФормуСпискаОпределяемогоТипа(ИмяОпределяемогоТипа) Экспорт
	
	ИмяФормы = канбан_КанбанДоскаВызовСервера.ИмяФормыСпискаОпределяемогоТипа(ИмяОпределяемогоТипа);
	
	Если Не ПустаяСтрока(ИмяФормы) Тогда
		ОткрытьФорму(ИмяФормы);
	КонецЕсли;
	
КонецПроцедуры

// События обработки оповещения.
// 
// Возвращаемое значение:
//  Массив из Строка
//
Функция СобытияОбработкиОповещения() Экспорт
	
	События = Новый Массив; // Массив из Строка
	События.Добавить("ОбновитьКанбанДоскуHTML");
	
	канбан_КанбанДоскаКлиентПереопределяемый.СобытияОбработкиОповещения(События);
	
	Возврат События;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Код процедур и функций

#КонецОбласти
