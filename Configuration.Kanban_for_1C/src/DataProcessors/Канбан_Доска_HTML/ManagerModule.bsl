// @strict-types


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получить текст HTML.
// 
// Возвращаемое значение:
//  Строка
//
Функция ТекстHTMLКанбанДоски() Экспорт
	
	МакетСтраницаРекламы = ПолучитьМакет("ШаблонКанбанДоскиHTML");
	ДокументHTML = МакетСтраницаРекламы.ПолучитьДокументHTML();
	УзелЧекбоксы = ДокументHTML.ПолучитьЭлементПоИдентификатору("checkboxes");
	
	ДанныеДляДоски = канбан_КанбанДоска.ДанныеДляКанбанДоски();
	ПроектыДоски = ДанныеДляДоски.Проекты;
	Для Каждого ДанныеПроекта Из ПроектыДоски Цикл
		
		УзелЧекбоксПроект = ДокументHTML.СоздатьЭлемент("Div");
		
		ИД_Проекта = СтрШаблон("project%1", ДанныеПроекта.ИдентификаторПроектаСтрока);
		
		//Input
		
		УзелИнпутЧекбокса = ДокументHTML.СоздатьЭлемент("input");
		УзелИнпутЧекбокса.УстановитьАтрибут("type", "checkbox");
		УзелИнпутЧекбокса.УстановитьАтрибут("id", ИД_Проекта);
//		УзелИнпутЧекбокса.УстановитьАтрибут("class", ИД_Проекта);
		УзелИнпутЧекбокса.УстановитьАтрибут("checked", "true");
		УзелИнпутЧекбокса.УстановитьАтрибут("onclick", "checkFluency(id, id)");
		УзелЧекбоксПроект.ДобавитьДочерний(УзелИнпутЧекбокса);
		
		//Label
		УзелЛейблЧекбокса = ДокументHTML.СоздатьЭлемент("label");
		УзелЛейблЧекбокса.УстановитьАтрибут("for", ИД_Проекта);
			
			//Вложенный div
			УзелЧекбоксИмя = ДокументHTML.СоздатьЭлемент("Div");
			УзелЧекбоксИмя.УстановитьАтрибут("class", СтрШаблон("tag %1", ИД_Проекта));
			УзелЧекбоксИмя.УстановитьАтрибут("style", СтрШаблон("background-color: rgb(%1);", ДанныеПроекта.Цвет));
			УзелЧекбоксИмя.ТекстовоеСодержимое = Строка(ДанныеПроекта.Проект); // TODO получить представление
			УзелЛейблЧекбокса.ДобавитьДочерний(УзелЧекбоксИмя);
		
		УзелЧекбоксПроект.ДобавитьДочерний(УзелЛейблЧекбокса);
		
		УзелЧекбоксы.ДобавитьДочерний(УзелЧекбоксПроект);
		
	КонецЦикла;
	
	УзелДоска = ДокументHTML.ПолучитьЭлементПоИдентификатору("kanban-board");

	Для Каждого ДанныеСтатуса Из ДанныеДляДоски.Статусы Цикл
		
		УзелРазделаСтатуса = ДокументHTML.СоздатьЭлемент("Div");
		УзелРазделаСтатуса.УстановитьАтрибут("class", "kanban-block");
		УзелРазделаСтатуса.УстановитьАтрибут("id", ДанныеСтатуса.УникальныйИдентификатор);
		УзелРазделаСтатуса.УстановитьАтрибут("ondragover", "allowDrop(event)");
		
		УзелРазделаИмяСтатуса = ДокументHTML.СоздатьЭлемент("strong");
		УзелРазделаИмяСтатуса.УстановитьАтрибут("class", "kanban-block__name");
		УзелРазделаИмяСтатуса.ТекстовоеСодержимое = СтрШаблон("%1 ", ДанныеСтатуса.Представление);
		
		УзелРазделаСтатуса.ДобавитьДочерний(УзелРазделаИмяСтатуса);
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Статус", ДанныеСтатуса.Статус);
		ЗадачиСоСтатусом = ДанныеДляДоски.ЗадачиИСтатусы.НайтиСтроки(ПараметрыОтбора);
		
		УзелРазделаКоличествоЗадач = ДокументHTML.СоздатьЭлемент("strong");
		УзелРазделаКоличествоЗадач.УстановитьАтрибут("class", "kanban-block__number");
		УзелРазделаКоличествоЗадач.ТекстовоеСодержимое = Строка(ЗадачиСоСтатусом.Количество());
		
		УзелРазделаСтатуса.ДобавитьДочерний(УзелРазделаКоличествоЗадач);
		УзелДоска.ДобавитьДочерний(УзелРазделаСтатуса);
		
		Для Каждого Задача Из ЗадачиСоСтатусом Цикл
			
			ИД_Задачи = СтрШаблон("task%1", Задача.УникальныйИдентификаторЗадача);
			ИД_Проекта = СтрШаблон("project%1", Задача.ИдентификаторПроектаСтрока);
			
			// Карточки
			УзелКарточка = ДокументHTML.СоздатьЭлемент("Div");
			УзелКарточка.УстановитьАтрибут("class", СтрШаблон("card %1 card__active", ИД_Проекта));
			
			УзелКарточка.УстановитьАтрибут("draggable", "true");
			УзелКарточка.УстановитьАтрибут("ondragstart", "drag(event)");
			УзелКарточка.УстановитьАтрибут("id", ИД_Задачи);
			
			#Область ШапкаКарточки
			
			УзелШапкаКарточки = ДокументHTML.СоздатьЭлемент("Div");
			УзелШапкаКарточки.УстановитьАтрибут("class", "card__header");
			
			УзелШапкаКарточкиСсылка = ДокументHTML.СоздатьЭлемент("a");
			УзелШапкаКарточкиСсылка.УстановитьАтрибут("class", "card__link");
			УзелШапкаКарточкиСсылка.УстановитьАтрибут("href", Задача.НавигационнаяСсылка);
			УзелШапкаКарточкиСсылка.ТекстовоеСодержимое = СтрШаблон("Задача №%1", Задача.Код);
			
			УзелШапкаКарточки.ДобавитьДочерний(УзелШапкаКарточкиСсылка);
			
			УзелШапкаКарточкиИзображение = ДокументHTML.СоздатьЭлемент("img");
			УзелШапкаКарточкиИзображение.УстановитьАтрибут("class", "card__photo");
			УзелШапкаКарточкиИзображение.УстановитьАтрибут("alt", "foto");
			УзелШапкаКарточкиИзображение.УстановитьАтрибут("src", Задача.ФотографияИсполнителя);
			
			УзелШапкаКарточки.ДобавитьДочерний(УзелШапкаКарточкиИзображение);
			
			УзелКарточка.ДобавитьДочерний(УзелШапкаКарточки);
			
			#КонецОбласти
			
			#Область ТелоКарточки
			
			УзелТелоКарточки = ДокументHTML.СоздатьЭлемент("div");
			УзелТелоКарточки.УстановитьАтрибут("class", "card__text");
			
			УзелТелоКарточкиТекст = ДокументHTML.СоздатьЭлемент("span");
			УзелТелоКарточкиТекст.ТекстовоеСодержимое = Задача.Наименование;
			
			УзелТелоКарточки.ДобавитьДочерний(УзелТелоКарточкиТекст);
			УзелКарточка.ДобавитьДочерний(УзелТелоКарточки);
			
			#КонецОбласти
			
			#Область ТэгКарточки
			
			УзелТэгКарточки = ДокументHTML.СоздатьЭлемент("div");
			УзелТэгКарточки.УстановитьАтрибут("class", "tag");
			УзелТэгКарточки.УстановитьАтрибут("style", СтрШаблон("background-color: rgb(%1);", Задача.ЦветПроекта));
			УзелТэгКарточки.ТекстовоеСодержимое = Строка(Задача.Проект); // TODO получать представление ссылки
			
			УзелКарточка.ДобавитьДочерний(УзелТэгКарточки);
			
			УзелКнопкиОбновлениеСтатуса = ДокументHTML.СоздатьЭлемент("button");
			УзелКнопкиОбновлениеСтатуса.УстановитьАтрибут("class", СтрШаблон("changeStatus %1", ИД_Задачи));
			УзелКнопкиОбновлениеСтатуса.УстановитьАтрибут("style", "visibility: hidden;");
			
			УзелКарточка.ДобавитьДочерний(УзелКнопкиОбновлениеСтатуса);
			
			#КонецОбласти
			
			УзелРазделаСтатуса.ДобавитьДочерний(УзелКарточка);
			
		КонецЦикла;
	
	КонецЦикла;
	
	Результат = ТекстHTMLИзОбъектаДокументHTML(ДокументHTML);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

// Код процедур и функций

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает текст HTML из объекта ДокументHTML.
//
// Параметры:
//  ДокументHTML  - ДокументHTML - документ, из которого будет извлекаться текст.
//
// Возвращаемое значение:
//   Строка   - текст HTML
//
Функция ТекстHTMLИзОбъектаДокументHTML(Знач ДокументHTML)
	
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьHTML = Новый ЗаписьHTML;
	ЗаписьHTML.УстановитьСтроку();
	ЗаписьDOM.Записать(ДокументHTML, ЗаписьHTML);
	
	Возврат ЗаписьHTML.Закрыть();
	
КонецФункции

#КонецОбласти

#КонецЕсли
