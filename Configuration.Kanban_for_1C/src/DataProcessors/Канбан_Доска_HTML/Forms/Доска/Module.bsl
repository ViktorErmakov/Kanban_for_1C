// @strict-types


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстHTMLКанбанДоскиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	События = канбан_КанбанДоскаКлиент.СобытияОбработкиОповещения();
	
	канбан_КанбанДоскаКлиентПереопределяемый.СобытияОбработкиОповещения(События);
	
	ДопустимоеСобытие = События.Найти(ИмяСобытия); // Строка
	Если ДопустимоеСобытие <> Неопределено Тогда
		
		Если ИмяСобытия = "ОбновитьКанбанДоскуHTML" Тогда
			
			КанбанДоска = "";
			Элементы.КанбанДоска.Документ.documentElement.innerHTML = КанбанДоска;
			
		КонецЕсли;
		
		ТекстHTMLКанбанДоски();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Канбан доска при нажатии.
// 
// Параметры:
//  Элемент - ПолеФормы - Элемент
//  ДанныеСобытия - Структура:
//  * Button - Структура:
//  ** type - Строка
//  ** id - Строка
//  ** className - Строка
//  ** parentElement - Структура:
//  *** id - Строка
//  *** parentElement - Структура:
//  **** id - Строка
//  * Element - Структура:
//  ** className - Строка
//      
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
&НаКлиенте
Процедура КанбанДоскаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Кнопка = ДанныеСобытия.Button;
	Если Не ЗначениеЗаполнено(Кнопка) 
		// Пропускаем обработку для быстрого фильтра проектов
		Или Кнопка.type = "checkbox" Тогда
		Возврат;
	КонецЕсли;
	
	Если Кнопка.id = "V8_request" Тогда
		
		Если Кнопка.value = "changeStatus" Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ИдентификаторЗадача = СтрЗаменить(Кнопка.attributes[3].nodeValue, "task", ""); // Строка
			ИдентификаторСтатуса = Кнопка.attributes[4].nodeValue; // Строка
			
			КанбанДоскаПриИзмененииСтатусаЗадаче(
				ИдентификаторЗадача, 
				ИдентификаторСтатуса);
			
			ОповеститьОбИзменении(ЗадачаСсылка);
			
		КонецЕсли;
		
	ИначеЕсли Кнопка.id = "update_svg" Тогда
		
		СтандартнаяОбработка = Ложь;
		ПодключитьОбработчикОжидания("ТекстHTMLКанбанДоски", 0.1, Истина);
		
	ИначеЕсли Кнопка.id = "setup_svg" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Обработка.Канбан_Доска_HTML.Форма.НастройкиДоски");
		
	ИначеЕсли Не ПустаяСтрока(Кнопка.className) Тогда
		
		КлассыЭлемента = СтрРазделить(Кнопка.className, " ");
		ОсновноеИмяКлассаКнопки = КлассыЭлемента[0];
		
		Если ОсновноеИмяКлассаКнопки = "add_task" Тогда
			
			СтандартнаяОбработка = Ложь;
			ИдентификаторСтатусаСтрока = СтрЗаменить(КлассыЭлемента[1], "status", "");
			СтатусЗадачиСсылка = СтатусЗадачиСсылка(ИдентификаторСтатусаСтрока);
			
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("Статус", СтатусЗадачиСсылка); // Управление задач не заполняет параметры, но может другая конфигурация будет.
			ФормаЗадачи = ОткрытьФорму("Справочник.узЗадачи.ФормаОбъекта", ПараметрыОткрытия);
			ФормаЗадачи.Открыть();
			
		ИначеЕсли ОсновноеИмяКлассаКнопки = "my_tasks" Тогда
			
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ТекстHTMLКанбанДоски()
	
	Если Не ПустаяСтрока(КанбанДоска) Тогда
		
		ЭлементКанбанДоска = Элементы.КанбанДоска.Документ;
		КанбанДоска = ЭлементКанбанДоска.documentElement.innerHTML;
		
	КонецЕсли;
	
	ТекстHTMLКанбанДоскиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТекстHTMLКанбанДоскиНаСервере()
	
	канбан_КанбанДоска.ТекстHTMLКанбанДоски(КанбанДоска, УникальныйИдентификатор);
	
КонецПроцедуры

// Канбан доска при изменении статуса задаче.
// 
// Параметры:
//  ИдентификаторЗадача - Строка
//  ИдентификаторСтатуса - Строка
//
&НаСервере
Процедура КанбанДоскаПриИзмененииСтатусаЗадаче(Знач ИдентификаторЗадача, Знач ИдентификаторСтатуса)
	
	Отказ = Ложь;
	ЗадачаСсылка = канбан_КанбанДоска.ИзменитьСтатусЗадаче(ИдентификаторЗадача, ИдентификаторСтатуса, Отказ);
	
	Если Не Отказ Тогда
		ТекстHTMLКанбанДоскиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СтатусЗадачиСсылка(ИдентификаторСтатусаСтрока)
	
	ТипОбъектаСтатус = Метаданные.ОпределяемыеТипы.канбан_СтатусыЗадач.Тип.Типы()[0];
	ПустаяСсылкаСтатус = Новый (ТипОбъектаСтатус); // ОпределяемыйТип.канбан_СтатусыЗадач
	МенеджерОбъектаСтатусы = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ПустаяСсылкаСтатус);
	
	ИдентификаторСтатуса = Новый УникальныйИдентификатор(ИдентификаторСтатусаСтрока);
	СтатусСсылка = МенеджерОбъектаСтатусы.ПолучитьСсылку(ИдентификаторСтатуса);
	
	Возврат СтатусСсылка;
	
КонецФункции

#КонецОбласти