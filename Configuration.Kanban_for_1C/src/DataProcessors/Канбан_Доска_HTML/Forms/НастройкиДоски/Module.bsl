// @strict-types


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КанбанДоскаHTML = канбан_КанбанДоска.КанбанДоскаHTMLВключенаЗагрузить();
	
	// Вынести в отдельный метод, и переделать на 
	НаборЗаписей = РеквизитФормыВЗначение("СтатусыЗадач");
	НаборЗаписей.Прочитать();
	ЗначениеВРеквизитФормы(НаборЗаписей, "СтатусыЗадач");
	
	ИспользуемыеПроектыДоскиЗагрузить();
	
	АдресЛоготипа = БиблиотекаКартинок.канбан_Доска;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("СохранитьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КанбанДоскаHTMLПриИзменении(Элемент)
	
	КанбанДоскаHTMLПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСтатусыЗадач

&НаКлиенте
Процедура СтатусыЗадачПриИзменении(Элемент)
	
	Номер = 1;
	Для Каждого СтрокаСтатуса Из СтатусыЗадач Цикл
		СтрокаСтатуса.НомерПоПорядку = Номер;
		Номер = Номер + 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИспользуемыеПроектыДоски

&НаКлиенте
Процедура ИспользуемыеПроектыДоскиЦветНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ИспользуемыеПроектыДоскиЦветНачалоВыбораПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуемыеПроектыДоскиЦветПриИзменении(Элемент)
	
	ИспользуемыеПроектыДоскиЦветПриИзмененииСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьПродолжение();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьПродолжение();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	Элементы.ГруппаСтатусыЗадач.ТолькоПросмотр = Не КанбанДоскаHTML;
	Элементы.ГруппаПроекты.ТолькоПросмотр = Не КанбанДоскаHTML;
	
	ИспользуемыеПроектыДоскиЦветПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ИспользуемыеПроектыДоскиЗагрузить()
	
	ПроектыЗагруженные = канбан_КанбанДоска.ИспользуемыеПроектыДоскиЗагрузить();
	Если ЗначениеЗаполнено(ПроектыЗагруженные) Тогда
		
		ПроектыРеквизит = РеквизитФормыВЗначение("ИспользуемыеПроектыДоски");
		Для Каждого СтрокаПроектаЗагруженного Из ПроектыЗагруженные Цикл
			
			НоваяСтрокаПроектыРеквизит = ПроектыРеквизит.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаПроектыРеквизит, СтрокаПроектаЗагруженного);
			
		КонецЦикла;
		ЗначениеВРеквизитФормы(ПроектыРеквизит, "ИспользуемыеПроектыДоски");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КанбанДоскаHTMLПриИзмененииНаСервере()
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьПродолжение()
	
	Отказ = Ложь;
	
	ЗаписатьНаСервере(Отказ);
	
	Если Не Отказ Тогда
		Модифицированность = Отказ;
	КонецЕсли;
	
	Оповестить("ОбновитьКанбанДоскуHTML");
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере(Отказ)
	
	канбан_КанбанДоска.КанбанДоскаHTMLВключенаСохранить(КанбанДоскаHTML, Отказ);
	ЗаписатьСтатусыЗадач(Отказ);
	ЗаписатьИспользуемыеПроектыДоски(Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСтатусыЗадач(Отказ)
	
	Попытка
		
		НаборЗаписей = РеквизитФормыВЗначение("СтатусыЗадач");
		НаборЗаписей.Записать();
		
	Исключение
		
		Отказ = Истина;
		
		ЗаписьЖурналаРегистрации(
			"Запись регистра сведений канбан_ИспользуемыеСтатусыДоски", 
			УровеньЖурналаРегистрации.Ошибка, , 
			"Не удалось записать набор записей", 
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИспользуемыеПроектыДоски(Отказ)
	
	Проекты = РеквизитФормыВЗначение("ИспользуемыеПроектыДоски");
	канбан_КанбанДоска.ИспользуемыеПроектыДоскиСохранить(Проекты, Отказ);

КонецПроцедуры

&НаКлиенте
Асинх Процедура ИспользуемыеПроектыДоскиЦветНачалоВыбораПродолжение()
	
	ТекущиеДанные = Элементы.ИспользуемыеПроектыДоски.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЦветДоВыбора = ТекущиеДанные.Цвет;
	
	ВыборЦвета = Новый ДиалогВыбораЦвета;
	ВыборЦвета.Цвет = ЦветИзСтроки(ЦветДоВыбора);
	Ждать ВыборЦвета.ВыбратьАсинх();
	
	АбсолютныйЦвет = ВыборЦвета.Цвет.ПолучитьАбсолютный();
	Красный = ?(АбсолютныйЦвет.Красный = 0, "0", АбсолютныйЦвет.Красный);
	Зеленый = ?(АбсолютныйЦвет.Зеленый = 0, "0", АбсолютныйЦвет.Зеленый);
	Синий = ?(АбсолютныйЦвет.Синий = 0, "0", АбсолютныйЦвет.Синий);
	
	ТекущиеДанные.Цвет = СтрШаблон("%1,%2,%3", Красный, Зеленый, Синий);
	
	Если ТекущиеДанные.Цвет <> ЦветДоВыбора Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
	ИспользуемыеПроектыДоскиЦветПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
Процедура ИспользуемыеПроектыДоскиЦветПриИзмененииСервер()
	
	Для Каждого СтрокаПроекта Из ИспользуемыеПроектыДоски Цикл
		
		Если Не ПустаяСтрока(СтрокаПроекта.Цвет) Тогда
			
			ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
			
			ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИспользуемыеПроектыДоски.Цвет");
			ЭлементОтбораДанных.ВидСравнения  = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбораДанных.ПравоеЗначение = СтрокаПроекта.Цвет;
			ЭлементОтбораДанных.Использование = Истина;
			
			ЭлементОформляемогоПоля = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
			ЭлементОформляемогоПоля.Поле = Новый ПолеКомпоновкиДанных("ИспользуемыеПроектыДоскиЦвет");
			ЭлементОформляемогоПоля.Использование = Истина;
			
			Цвет = ЦветИзСтроки(СтрокаПроекта.Цвет);
			
			ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", Цвет);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ЦветИзСтроки(ЦветСтрокой)
	
	Если ПустаяСтрока(ЦветСтрокой) Тогда
		
		Цвет = Новый Цвет;
		
	Иначе
		
		Цвета = СтрРазделить(ЦветСтрокой, ",");
		Цвет = Новый Цвет(Цвета[0], Цвета[1], Цвета[2]);
		
	КонецЕсли;
	
	Возврат Цвет;
	
КонецФункции

&НаКлиенте
Процедура СохранитьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ЗаписатьПродолжение();
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
