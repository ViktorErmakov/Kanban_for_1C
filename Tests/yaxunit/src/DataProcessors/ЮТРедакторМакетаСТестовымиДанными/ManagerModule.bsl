#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Преобразует markdown-таблицы в табличный документ
// с сохранением группировки таблиц по областям.
// 
// Параметры:
//  ТекстовыйДокумент - ТекстовыйДокумент
// 
// Возвращаемое значение:
//  ТабличныйДокумент
//  
Функция КонвертироватьВТабличныйДокумент(ТекстовыйДокумент) Экспорт
	
	Результат = Новый ТабличныйДокумент();
	
	СписокОбластей = КоллекцияТаблицИзТекста(ТекстовыйДокумент);
	
	МакетРазделителя = МакетРазелителяТаблиц();
	
	Для НомерОбласти = 1 По СписокОбластей.Количество() Цикл
		
		Если НомерОбласти > 1 Тогда
			Результат.Вывести(МакетРазделителя);
		КонецЕсли;
		
		МакетОбласти = СписокОбластей[НомерОбласти - 1].Значение;  // ТабличныйДокумент
		ИмяОбласти = СписокОбластей[НомерОбласти - 1].Представление;
		
		ИзменитьШиринуПоСодержимому(МакетОбласти);
		
		ВывестиВТабличныйДокумент(Результат, МакетОбласти, ИмяОбласти);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Преобразует содержимое табличного документа в markdown-таблицы
// с сохранением группировки таблиц по областям.
// 
// Параметры:
//  ТабличныйДокумент - ТабличныйДокумент
// 
// Возвращаемое значение:
//  Строка
//  
Функция КонвертироватьВТекстовыйДокумент(ТабличныйДокумент) Экспорт

	Если ТабличныйДокумент.Области.Количество() = 0 Тогда
		
		ТекстовыйДокумент = ОбластьТабличногоМакетаВТекст(ТабличныйДокумент);
		
	Иначе
		
		Результат = Новый Массив();
		
		Для Каждого ОбластьМакета Из ТабличныйДокумент.Области Цикл
			
			Документ = ТабличныйДокумент.ПолучитьОбласть(ОбластьМакета.Имя);
			
			ТаблицаТекстом = ОбластьТабличногоМакетаВТекст(Документ, ОбластьМакета.Имя);
			
			Результат.Добавить(ТаблицаТекстом);
			
		КонецЦикла;
		
		ТекстовыйДокумент = СтрСоединить(Результат, Символы.ПС + Символы.ПС);
		
	КонецЕсли;
	
	Возврат ТекстовыйДокумент;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КонвертацияВТабличныйДокумент

Функция КоллекцияТаблицИзТекста(Текст)
	
	СписокОбластей = Новый СписокЗначений();  // СписокЗначений из ТабличныйДокумент
	
	МакетОбласти = Неопределено;
	ИмяОбластиМакета = "";
	
	Для Каждого ТекущаяСтрока Из СтрРазделить(Текст, Символы.ПС) Цикл
		
		Если ПустаяСтрока(ТекущаяСтрока) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяСтрока = СокрЛП(ТекущаяСтрока);
		
		Если СтрНачинаетсяС(ТекущаяСтрока, "#Область ") Тогда
			
			Если МакетОбласти = Неопределено Тогда
				ИмяОбластиМакета = Сред(ТекущаяСтрока, 10);				
			КонецЕсли;
			
		ИначеЕсли ТекущаяСтрока = "#КонецОбласти" Тогда
			
			Если МакетОбласти <> Неопределено Тогда
				СписокОбластей.Добавить(МакетОбласти, ИмяОбластиМакета);
				
				МакетОбласти = Неопределено;
				ИмяОбластиМакета = "";
			КонецЕсли;
			
		ИначеЕсли МакетОбласти <> Неопределено
			И МакетОбласти.ВысотаТаблицы = 1
			И СтрРазделить(ТекущаяСтрока, " -|", Ложь).Количество() = 0 Тогда
			
			// Игорируем разделитель шапки и содержимого таблицы.
			
		Иначе
			
			Если МакетОбласти = Неопределено Тогда
				МакетОбласти = Новый ТабличныйДокумент();
			КонецЕсли;
			
			Если СтрНачинаетсяС(ТекущаяСтрока, "|") И СтрЗаканчиваетсяНа(ТекущаяСтрока, "|") Тогда
				ТекущаяСтрока = Сред(ТекущаяСтрока, 2, СтрДлина(ТекущаяСтрока) - 2);
			КонецЕсли;
			
			НомерСтроки = МакетОбласти.ВысотаТаблицы + 1;
			ЗначенияЯчеек = СтрРазделить(ТекущаяСтрока, "|", Истина);
			
			Для Индекс = 0 По ЗначенияЯчеек.ВГраница() Цикл
				
				НомерКолонки = Индекс + 1;
				Ячейка = МакетОбласти.Область(НомерСтроки, НомерКолонки);
				Ячейка.Текст = СокрЛП(ЗначенияЯчеек[Индекс]);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если МакетОбласти <> Неопределено Тогда
		СписокОбластей.Добавить(МакетОбласти, ИмяОбластиМакета);
	КонецЕсли;
	
	Возврат СписокОбластей
	
КонецФункции

Функция МакетРазелителяТаблиц()
	
	Макет = Новый ТабличныйДокумент();
	Макет.Область(1, 1).Текст = "";
	
	Возврат Макет;
	
КонецФункции

Процедура ИзменитьШиринуПоСодержимому(Документ)
	
	МинимальнаяШирина = 5;
	МаксимальнаяШирина = 25;
	
	Для НомерКолонки = 1 По Документ.ШиринаТаблицы Цикл
		
		ШиринаВСимволах = МаксимальнаяДлиннаТекстаВКолонке(Документ, НомерКолонки);
		ШиринаВТочках = Мин(Макс(МинимальнаяШирина, ШиринаВСимволах), МаксимальнаяШирина);
		
		Колонка = Документ.Область(, НомерКолонки, , НомерКолонки);
		Колонка.ШиринаКолонки = ШиринаВТочках;
		
	КонецЦикла;
	
КонецПроцедуры

Функция МаксимальнаяДлиннаТекстаВКолонке(Документ, НомерКолонки)
	
	Результат = 0;
	
	Для НомерСтроки = 1 По Документ.ВысотаТаблицы Цикл
		
		ТекстЯчейки = Документ.Область(НомерСтроки, НомерКолонки).Текст;
		Результат = Макс(Результат, СтрДлина(ТекстЯчейки));
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ВывестиВТабличныйДокумент(Документ, МакетОбласти, ИмяОбласти = "")
	
	НачалоВывода = Документ.ВысотаТаблицы + 1;
	
	Документ.Вывести(МакетОбласти);
	
	КонецВывода = Документ.ВысотаТаблицы;
	
	ОбластьШапки = Документ.Область(НачалоВывода, , НачалоВывода);
	ОбластьШапки.Шрифт = Новый Шрифт(ОбластьШапки.Шрифт, , , Истина);  //@skip-check new-font
	
	Если Не ПустаяСтрока(ИмяОбласти) Тогда
		
		ОбластьМакета = Документ.Область(НачалоВывода, , КонецВывода, );
		ОбластьМакета.Имя = ИмяОбласти;
		ОбластьМакета.СоздатьФорматСтрок();
		
	КонецЕсли;
	
	Для НомерКолонки = 1 По МакетОбласти.ШиринаТаблицы Цикл
		
		КолонкаИсточника = МакетОбласти.Область(, НомерКолонки, , НомерКолонки);
		КолонкаПриемника = Документ.Область(НачалоВывода, НомерКолонки, КонецВывода, НомерКолонки);
		КолонкаПриемника.ШиринаКолонки = КолонкаИсточника.ШиринаКолонки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область КонвертацияВТекстовыйДокумент

Функция ОбластьТабличногоМакетаВТекст(Документ, Имя = "")
	
	Результат = Новый Массив();
	
	Если Не ПустаяСтрока(Имя) Тогда
		
		Результат.Добавить("#Область " + Имя);
		Результат.Добавить("");
		
	КонецЕсли;
	
	Если Документ.ВысотаТаблицы > 0 Тогда
		
		ТаблицаДанных = ОбластьТабличногоМакетаВТаблицуЗначений(Документ);
		
		РассчитатьШиринуКолонок(ТаблицаДанных);
		
		ВывестиИменаКолонокВТекстовыйДокумент(Результат, ТаблицаДанных);
		ВывестиРазделительШапкиВТекстовыйДокумент(Результат, ТаблицаДанных);
		
		Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
			ВывестиЗначенияКолонокВТекстовыйДокумент(Результат, СтрокаТаблицы);
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(Имя) Тогда
		
		Результат.Добавить("");
		Результат.Добавить("#КонецОбласти");
		
	КонецЕсли;
	
	Возврат СтрСоединить(Результат, Символы.ПС);
	
КонецФункции

Функция ОбластьТабличногоМакетаВТаблицуЗначений(Документ)
	
	ТаблицаДанных = Новый ТаблицаЗначений();
	
	Для НомерКолонки = 1 По Документ.ШиринаТаблицы Цикл
		
		ИмяКолонки = "_" + Формат(НомерКолонки, "ЧН=; ЧГ=;");
		ЗаголовокКолонки = Документ.Область(1, НомерКолонки).Текст;
		ТипКолонки = Новый ОписаниеТипов("Строка");
		
		ТаблицаДанных.Колонки.Добавить(ИмяКолонки, ТипКолонки, ЗаголовокКолонки);
		
	КонецЦикла;
	
	Для НомерСтроки = 2 По Документ.ВысотаТаблицы Цикл
		
		НоваяСтрока = ТаблицаДанных.Добавить();
		
		Для НомерКолонки = 1 По Документ.ШиринаТаблицы Цикл
			
			НоваяСтрока[НомерКолонки - 1] = Документ.Область(НомерСтроки, НомерКолонки).Текст;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

Процедура РассчитатьШиринуКолонок(ТаблицаДанных)
	
	Для Каждого Колонка Из ТаблицаДанных.Колонки Цикл
		
		Колонка.Ширина = СтрДлина(Колонка.Заголовок);
		
		Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
			Значение = СтрокаТаблицы[Колонка.Имя];
			Колонка.Ширина = Макс(Колонка.Ширина, СтрДлина(Значение));
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиИменаКолонокВТекстовыйДокумент(Результат, ТаблицаДанных)
	
	ТекущаяСтрока = "|";
	
	Для Каждого Колонка Из ТаблицаДанных.Колонки Цикл
		
		ТекущаяСтрока = ТекущаяСтрока
			+ " "
			+ ДополнитьСтроку(Колонка.Заголовок, Колонка.Ширина)
			+ " |";
		
	КонецЦикла;
	
	Результат.Добавить(ТекущаяСтрока);
	
КонецПроцедуры

Процедура ВывестиРазделительШапкиВТекстовыйДокумент(Результат, ТаблицаДанных)
	
	ТекущаяСтрока = "|";
	
	Для Каждого Колонка Из ТаблицаДанных.Колонки Цикл
		
		ТекущаяСтрока = ТекущаяСтрока
			+ ДополнитьСтроку("", Колонка.Ширина + 2, "-")
			+ "|";
		
	КонецЦикла;
	
	Результат.Добавить(ТекущаяСтрока);
	
КонецПроцедуры

Процедура ВывестиЗначенияКолонокВТекстовыйДокумент(Результат, СтрокаТаблицы)
	
	ТекущаяСтрока = "|";
	
	Для Каждого Колонка Из СтрокаТаблицы.Владелец().Колонки Цикл
		
		ТекущаяСтрока = ТекущаяСтрока
			+ " "
			+ ДополнитьСтроку(СтрокаТаблицы[Колонка.Имя], Колонка.Ширина)
			+ " |";
		
	КонецЦикла;
	
	Результат.Добавить(ТекущаяСтрока);
	
КонецПроцедуры

Функция ДополнитьСтроку(ИсходнаяСтрока, ОжидаемаяДлина, Символ = " ")
	
	ДлинаСтроки = СтрДлина(ИсходнаяСтрока);
	ДополнениеСтроки = "";
	
	Для Счетчик = 1 По (ОжидаемаяДлина - ДлинаСтроки) Цикл
		ДополнениеСтроки = ДополнениеСтроки + Символ;
	КонецЦикла;
	
	Возврат ИсходнаяСтрока + ДополнениеСтроки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли